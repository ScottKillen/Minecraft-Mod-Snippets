task echoVersion << {
    printNextVersion()
}

configure(echoVersion) {
    group = "Versioning"
    description = 'Echo the version number to the console'
}

def saveVersion() {
    ant.propertyfile(
            file: "gradle.properties") {
        entry( key:"version_major", type:"int", value: version_major)
        entry( key:"version_series", type:"int", value: version_series)
        entry( key:"version_revision", type:"int", value: version_revision)
    }
    rootProject.version = "$version_mc-$version_major.$version_series.$version_revision"
    printNextVersion()
}

def printNextVersion(){
    println '*************************************************************'
    println 'The next build will be version: ' +  rootProject.version
    println '*************************************************************'
}

task bumpMajor << {
    project.version_major = Integer.parseInt(project.version_major) + 1
    project.version_series = "0"
    project.version_revision = "0"
    saveVersion()
}

configure(bumpMajor) {
    group = "Versioning"
    description = 'Increment the leftmost version number'
}

task bumpSeries << {
    project.version_series = Integer.parseInt(project.version_series) + 1
    project.version_revision = "0"
    saveVersion()
}

configure(bumpSeries) {
    group = "Versioning"
    description = 'Increment the middle version number'
}

task bumpRevision << {
    project.version_revision = Integer.parseInt(project.version_revision) + 1
    saveVersion()
}

configure(bumpRevision) {
    group = "Versioning"
    description = 'Increment the rightmost version number'
}
